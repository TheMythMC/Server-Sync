import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
}

version = project.mod_version
group = project.maven_group

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://maven.shedaniel.me'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include(modApi("me.sargunvohra.mcmods:autoconfig1u:3.3.1")) {
        exclude group: "net.fabricmc.fabric-api", module: "fabric-api"
    }
    include(modApi("me.shedaniel.cloth:cloth-config-fabric:5.0.34")) {
        exclude group: "net.fabricmc.fabric-api", module: "fabric-api"
    }

    // discord wrapper

    modImplementation 'com.discord4j:discord4j-core:3.2.0'
    /**
     * This needs to be only implementation when running a production build, otherwise it needs to be modImplementation to
     * work in dev env i think.
     */
    // implementation 'com.discord4j:discord4j-core:3.2.0'
    shadow 'com.discord4j:discord4j-core:3.2.0'

}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 16
def sourceJavaVersion = 16
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

compileJava {
    sourceCompatibility = '16'
    targetCompatibility = '16'
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}
/**
 * DONT TOUCH THIS
 *  IT LOOKS LIKE ITS NOT BEING USED BUT DONT TAKE THE CHANCE
 *  I TRUST YOU
 */
task remapShadowJar(type: RemapJarTask) {
    dependsOn shadowJar
    input = shadowJar.archiveFile
    addNestedDependencies.set(true)
}

build {
    dependsOn remapShadowJar
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    finalizedBy "shadowJar"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
        // mavenLocal()
    }

}

shadowJar {
    destinationDirectory = file("${buildDir}/tmp/allJar")
    configurations = [project.configurations.shadow]
    relocate 'io.netty', 'shaded.io.netty'

    //Exclude META-INF entries of shaded deps
    exclude 'META-INF/**'
    from('.') {
        include 'LICENSE.md'
        include 'NOTICE.txt'
        include 'licenses/**'
    }
    minimize()
}
